module

public import Std.Data.TreeSet.Basic
public meta import Std.Data.TreeSet.Basic

public import EvmYul.Maps.StorageMap
public meta import EvmYul.Maps.StorageMap
public import EvmYul.SpongeHash.Keccak256
public meta import EvmYul.SpongeHash.Keccak256

public import EvmYul.UInt256
public meta import EvmYul.UInt256
public import EvmYul.Wheels
public meta import EvmYul.Wheels

public import EvmYul.Yul.Ast
public meta import EvmYul.Yul.Ast

@[expose] public section

namespace EvmYul

/--
  Precompiled contract addresses.
  (142) `π ≡ {1, 2, 3, 4, 5, 6, 7, 8, 9, 10}`
-/
def π : Std.TreeSet AccountAddress compare :=
  Std.TreeSet.ofList ((List.range 11).tail.map (Fin.ofNat _)) compare

inductive ToExecute (τ : OperationType) where
  | Code (code : Yul.Ast.contractCode τ)
  | Precompiled (precompiled : AccountAddress)

structure PersistentAccountState (τ : OperationType) where
  nonce    : UInt256
  balance  : UInt256
  storage  : Storage
  code     : (Yul.Ast.contractCode τ)
  deriving BEq, Inhabited, Repr

/--
The `Account` data. Section 4.1.

Suppose `a` is some address.

- `nonce`    -- σ[a]ₙ.
- `balance`  -- σ[a]_b.

In the yellow paper it is supposed to be a 256-bit hash of the root node of
a Merkle Tree. KEVM implemets it as just an key/value map.
- `storage`  -- σ[a]_s.
- `tstorage` -- Transiont storage; added in EIP-1153
- `codeHash` -- σ[a]_c.

For now, we assume no global map `GM` with which `GM[code_hash] ≡ code`.
- `code`
-/
structure Account (τ : OperationType) extends PersistentAccountState τ where
  tstorage : Storage
deriving BEq, Inhabited

def PersistentAccountState.codeHash (self : PersistentAccountState .EVM) : UInt256 :=
  .ofNat <| fromByteArrayBigEndian (ffi.KEC self.code)

def Account.codeHash (self : (Account .EVM)) : UInt256 :=
  self.toPersistentAccountState.codeHash

end EvmYul
